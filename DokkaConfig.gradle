apply plugin: 'org.jetbrains.dokka-android'

dokka {
  moduleName = ""

  outputDirectory = "$rootDir/docs"
  outputFormat = "gfm"

  includes = ['extra.md']
  samples = ['src/sample']

  includeNonPublic = false
  skipDeprecated = false
  reportUndocumented = false
  skipEmptyPackages = true

  externalDocumentationLink {
    url = new URL("https://docs.oracle.com/javase/7/docs/api/")
  }
  externalDocumentationLink {
    url = new URL("http://reactivex.io/RxJava/javadoc/")
  }
  externalDocumentationLink {
    url = new URL("http://jakewharton.github.io/timber/")
  }
  externalDocumentationLink {
    url = new URL("https://square.github.io/retrofit/2.x/retrofit/")
  }
  externalDocumentationLink {
    url = new URL("https://square.github.io/okhttp/3.x/okhttp/")
  }

  def appPath = new File(project.projectDir, "/src/main/java")
  def relativeAppPath = rootDir.toPath().relativize(appPath.toPath()).toString()
  linkMapping {
    dir = appPath
    url = "https://github.com/asvid/NestedList/tree/master/$relativeAppPath"
    suffix = "#L"
  }

  Set<ProjectDependency> deps =
      project.configurations.collectMany {
        it.allDependencies
      }.findAll {
        it instanceof ProjectDependency
      }

  sourceDirs = files(deps.collect {
    p ->
      def path = new File(p.getDependencyProject().projectDir, "/src/main/java")
      def relativePath = rootDir.toPath().relativize(path.toPath()).toString()
      linkMapping {
        dir = path
        url = "https://github.com/asvid/NestedList/tree/master/$relativePath"
        suffix = "#L"
      }
      return path
  })
}